<html>

  <head>
      <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
      <title>Perry the flapping platypus ; ) </title>
      <script type="text/javascript" src="js/processing.js"></script>
  </head>
  
  <body>
    <h1> FLAPPING PERRY<span style="color:blue"> </h1>
    <p> Help Perry the Platypus Save The TriState Area!!! ;) </p>
    
    <script type="text/processing">

      PImage perry;
PImage bg, bg2;
PImage pipeT, pipeB;
int bgx;
int y, Vy;
int[] px, py;
boolean gameState;
int score, highScore;
//SoundFile flapSound;

void setup() {
  size(1280, 720);
  bg = loadImage("back2.png");
  bg2 = loadImage("ground2.png");
  perry = loadImage("Perry.png");
  pipeT = loadImage("pipeT.png");
  pipeB = loadImage("pipeB.png");
 
  //flapSound = new SoundFile(this, "flap.mp3");

  resetGame();
}

void draw() {
  if (gameState) {
    drawBG();
    drawPipes();
    drawPerry();
    checkCrash();
    updateScore();
    displayScore();
  } else {
    displayGameOver();
  }
}

void resetGame() {
  bgx = 0;
  y = height / 2;
  Vy = 0;
 
  px = new int[3];
  py = new int[3];
  for (int i = 0; i < 3; i++) {
    px[i] = width + width / 3 * i;
    py[i] = int(random(200, height - 200));
  }

  score = 0;
  gameState = true;
}

void drawPipes() {
  for (int i = 0; i < px.length; i++) {
    px[i] -= 3;
    image(pipeT, px[i] - 25, py[i] - pipeT.height - 100);
    image(pipeB, px[i] - 25, py[i] + 100);

    // Reset pipe position and update score if passed
    if (px[i] < -50) {
      px[i] = width + 50;
      py[i] = int(random(100, height - 100));
      score++;
    }
  }
}

void drawPerry(){
  image(perry, 50, y);
  y += Vy;
  Vy++;
  if(y > height-perry.height){
    y = height-perry.height;
  }
}

void checkCrash(){
  for(int i = 0; i < px.length; i++){
    //Bottom
    if(px[i] < 73+ perry.width && px[i] > 30 && y - py[i] > 30){
      gameState = false;
    }
    //top
    if(px[i] < 70 + perry.width && px[i] > 40 && py[i] - y > 110){
      gameState = false;
    }
  }
}

void mousePressed() {
  if (gameState) {
    Vy = -10;
    //flapSound.play();
  } else {
    resetGame();
  }
}

void drawBG() {
  image(bg, bgx, 0);
  image(bg2, bgx + bg.width, 0);
  image(bg, bgx + 2 * bg.width, 0);
  bgx -= 1;
  if (bgx < -2 * bg.width) {
    bgx = 0;
  }
}

void updateScore() {
  if (score > highScore) {
    highScore = score;
  }
}

void displayScore() {
  fill(160, 160, 160, 200);
  rect(width - 175, 10, 155, 80, 5);
  fill(0);
  textSize(32);
  text("Score: " + score, width - 170, 40);
  text("High: " + highScore, width - 170, 80);
}

void displayGameOver() {
  textSize(60);
  fill(0);
  text("You Lost!", 450, 250);
  text("Click anywhere to play again!", 250, 400);
}
          </script>
    <canvas id="sketch" style="border: 1px solid black;"></canvas>
    
  </body>
  
</html>
